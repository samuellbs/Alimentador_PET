# Projeto de Alimentador para C√£es ou Gatos

Esse trabalho aborda o desenvolvimento de um alimentador autom√°tico para animais dom√©sticos utilizando ESP-32 com RTC.
Autores: Samuel Barros Souza   (RA:20.01044-3);
         Lucas Granja Bernardo (RA:19.00305-6);

# üöÄ Introdu√ß√£o

A proposta desse projeto √© o desenvolvimento de um sistema autom√°tico para libera√ß√£o de comida em hor√°rios determinados via c√≥digo. Como tamb√©m, √© a implementa√ß√£o de um bot√£o que permita que o usu√°rio despeje a comida para seu animal de estima√ß√£o quando desejar. 

# üíª Componentes Eletr√¥nicos e Esquema El√©trico 

O software utilizado para o desenvolvimento do esquem√°tico foi o Altium, e √© poss√≠vel observar na figura abaixo as liga√ß√µes.

![image](https://github.com/samuellbs/Alimentador_PET/assets/103770785/8689b729-66f0-4c9b-b2c6-f63c6623cd45)

A placa final desenvolvida n√£o utilizou um diodo em paralelo com o motor, mas √© recomend√°vel utilizar para evitar a tens√£o reversa no transistor. 

A tabela abaixo indica os componentes utilizados, bem como os pre√ßos deles (25/05/2023) como forma de realizar um levantamento de custo.

![image](https://github.com/samuellbs/Alimentador_PET/assets/103770785/cd5911a6-a360-4e6c-b6f2-1e72f2a021fb)


#  ‚öôÔ∏è Funcionamento do sistema

O projeto apresenta um display OLED I2C que indica o hor√°rio atual. Dessa forma, quando o bot√£o √© apertado, ocorrem duas situa√ß√µes simult√¢neas. A primeira √© a satura√ß√£o do transistor BC548 permitindo que o LED acenda. Como tamb√©m, o pino D18 do ESP32 est√° "monitorando" o acionamento do bot√£o, para que exiba no display "Hora da comida" e acione o motor para libera√ß√£o da comida. √â importante ressaltar que a proposta do bot√£o √© permitir que o usu√°rio despeje a comida quando quiser.

Por outro lado, como a proposta do sistema √© ser um alimentador pet autom√°tico, atrav√©s do c√≥digo √© poss√≠vel definir hor√°rios para que a comida seja despejada no pote. No caso do software original, est√£o definidos os hor√°rios 07:30:00 e 19:30:00. As imagens abaixo s√£o dedicadas para o funcionamento real do projeto, incluindo a placa final soldada, estrutura f√≠sica e exemplos do funcionamento descrito acima.

![image](https://github.com/samuellbs/Alimentador_PET/assets/103770785/db1bf6cf-6dea-44c6-ac03-a2b49627749c)

https://github.com/samuellbs/Alimentador_PET/assets/103770785/105a38ab-1e81-4e2a-8d41-1488f6c5eada

https://github.com/samuellbs/Alimentador_PET/assets/103770785/1b54e7d9-1f1e-4d01-a8e0-5c319d7db13d

√â importante ressaltar que a estrutura f√≠sica escolhida, n√£o foi desenvolvida pelos autores desse projeto. A escolha foi por uma estrutura da UsinaINFO, que atendia as necessidades.

# C√≥digo do sistema

O software foi desenvolvido na Arduino IDE.

# üìã Pr√©-requisitos para o Software

O c√≥digo necessita da instala√ß√£o de bibliotecas para o funcionamento de componentes do projeto.

# üìÑ Bibliotecas Diplay OLED I2C

Inicialmente, na IDE Arduino, selecione Ferramentas --> Gerenciar Bibliotecas. Logo depois, digite SSD1306 na barra de pesquisa e procure pela biblioteca SSD1306 do Adafruit e instale. Por fim, repita o processo e instale tamb√©m as bibliotecas GFX e BusIO, ambas do Adafruit.

# üìÑ Biblioteca Real Time Clock (RTC)

Acesse o link https://github.com/adafruit/RTClib e baixe o zip da biblioteca. Logo ap√≥s, clique em Sketch --> Incluir Biblioteca --> Adicionar Biblioteca .zip e selecione o arquivo baixado.

# ‚å®Ô∏è Software Vers√£o 0

```
/*    Programa: Alimentador Autom√°tico PET 
 *    Autores: Samuel Barros Souza   (RA: 20.01044-3)
 *             Lucas Granja Bernardo (RA: 19.00305-6)
 *             
 *    Vers√£o: 0
 *    Conclu√≠do em: 10/05/2023
 *    Breve Descri√ß√£o:
 *        Este programa realiza a libera√ß√£o de ra√ß√£o para animais
 *        dom√©sticos, utilizando hor√°rio real de Bras√≠lia ou pela 
 *        defini√ß√£o do usu√°rio.
 */
 
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include "RTClib.h"
#include <Fonts/FreeSerif9pt7b.h>


#define SCREEN_WIDTH 128 // OLED display width, in pixels
#define SCREEN_HEIGHT 64 // OLED display height, in pixels
// Declaration for an SSD1306 display connected to I2C (SDA, SCL pins)
#define OLED_RESET     -1 // Reset pin # (or -1 if sharing Arduino resetpin)
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);


#define button_pin 18       // pino do bot√£o
#define motor_pin_one 32   // pino 1 do motor
#define motor_pin_two 33   // pino 2 do motor



//RTC_DS1307 rtc; //RTC da classe DS1307
RTC_DS3231 rtc; //RTC da classe DS3132
  


int leituraButton;
int hora, minuto, segundo;
char strBuf[50];         // para montar a string com duas casas decimais



void setup()
{
  Serial.begin(115200);
  
  pinMode(motor_pin_one, OUTPUT);
  pinMode(motor_pin_two, OUTPUT);
  
  rtc.begin();
  //rtc.adjust(DateTime(F(__DATE__), F(__TIME__)));  // Atualiza o hor√°rio automaticamente, necess√°rio na primeira compila√ß√£o e depois comentar
  
  if (! rtc.begin()) {                         //Se o RTC nao for inicializado, faz
    Serial.println("RTC NAO INICIALIZADO");    //Imprime o texto
    while (1);                                 //Trava o programa
  }
  delay(100);                                  
  
  //rtc.adjust(DateTime(2023, 4, 19, 20, 59, 00));//Ajusta o tempo do RTC para a data e hora definida pelo usuario.
  
  if (!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {     // Identificar o Display
    Serial.println(F("SSD1306 allocation failed"));
    for (;;); // Caso n√£o encontre, loop infinito
  }
  
}

void loop() {

 
 OLED_display(); 


}


void OLED_display () {


/*      Descri√ß√£o de funcionamento:
 *       
 *          Essa √© a fun√ß√£o principal do c√≥digo e √© respons√°vel por 
 *          escrever no display o hor√°rio e monitorar as fun√ß√µes do
 *          bot√£o e do tempo.
 */
   
  DateTime agora = rtc.now();    
  hora = agora.hour();        // defini√ß√£o da hora atual
  minuto = agora.minute();    // defini√ß√£o do minuto atual
  segundo = agora.second();   // defini√ß√£o do segundo atual

  
  digitalWrite(motor_pin_one, LOW);   // motor desligado
  digitalWrite(motor_pin_two,LOW);   // motor desligado
  
  display.setFont(&FreeSerif9pt7b);   // defini√ß√£o da fonte de letra para display
  display.clearDisplay();
  // Display Text
  display.setTextSize(1);             // defini√ß√£o tamanho de letra para display
  display.setTextColor(WHITE);        // defini√ß√£o de cor de letra para display
  display.setCursor(0, 24);
  display.println("Horario");
  sprintf(strBuf, "%02d : %02d : %02d\n", hora, minuto, segundo);  // imprime no display "Horario: hora : minuto : segundo")
  display.println(strBuf);
  Serial.println(strBuf);
//  display.print(hora, DEC);
//  display.print(" : ");
//  display.print(minuto);
//  display.print(" :  ");
//  display.println(segundo);
  display.display();
  display.clearDisplay();
  
  trata_botao();          // monitoramento da fun√ß√£o do bot√£o  
  trata_tempo();          //monitoramento da fun√ß√£o do tempo
}



void OLED_event () {


/*      Descri√ß√£o de funcionamento: 
 *        
 *          Essa fun√ß√£o √© respons√°vel por realizar a mudan√ßa de funcionamento do sistema
 *          quando h√° eventos importantes (bot√£o HIGH ou tempo HIGH). Dessa maneira, ela
 *          controla o motor para despejar ra√ß√£o quando necess√°rio.
 *  
 */

  display.setFont(&FreeSerif9pt7b);   // fonte de letra do display
  display.clearDisplay();
  display.setTextSize(1);             // tamanho de letra do display
  display.setTextColor(WHITE);        // cor de letra do display
  display.setCursor(0, 24);
  display.println("Hora da comida");
  display.display();
  trata_motor();                      // monitoramento da fun√ß√£o do motor
  display.clearDisplay();

}


void trata_tempo() {


/*      Descri√ß√£o de funcionamento: 
 *        
 *          Essa fun√ß√£o √© respons√°vel por monitorar a igualdade do hor√°rio real com as
 *          defini√ß√µes abaixo para despejar a ra√ß√£o.
 *          Os hor√°rios de 07:30 e 19:30 foram definidos pelos autores do projeto e 
 *          s√£o totalmente arbitr√°rios.
 *  
 */
  if (hora == 07 && minuto == 30 && segundo == 00){

    OLED_event();
    delay(10000);
   
    
  }

  else if (hora == 19 && minuto == 30 && segundo == 00){

    OLED_event();
    delay(10000);
   
  }
}
/*
  else if (hora == 17 && minuto == 00 && segundo == 0) {

    OLED_event();
    delay(10000);
    
  }

else if (hora == 21 && minuto == 00 && segundo == 0){

    OLED_event();
    delay(10000);
    
  }
  digitalWrite(motor_pin,LOW);
}
*/


void trata_botao() {


/*      Descri√ß√£o de funcionamento: 
 *        
 *          Essa fun√ß√£o √© respons√°vel por monitorar o acionamento do bot√£o e acionar o motor para despejar ra√ß√£o.
 *  
 */
 
   leituraButton = digitalRead(button_pin);  // vari√°vel para leitura (HIGH ou LOW) do bot√£o 
   
  if (leituraButton == HIGH){

    OLED_event();
    delay(10000);
  
  }
  
  leituraButton = 0;
}

void trata_motor() {

/*      Descri√ß√£o de funcionamento: 
 *        
 *          Essa fun√ß√£o √© respons√°vel por acionar o motor quando desejado, conforme 
 *          os eventos de tempo ou bot√£o.
 *          
 *  
 */
 
    digitalWrite(motor_pin_one, HIGH);
    digitalWrite(motor_pin_two, LOW);         // motor no sentido anti-hor√°rio (10)
    delay(3000);
   
    digitalWrite(motor_pin_one, LOW);
    digitalWrite(motor_pin_two, HIGH);      // motor no sentido hor√°rio (01)
    delay(2000);
    
    digitalWrite(motor_pin_one, HIGH);
    digitalWrite(motor_pin_two, LOW);       // motor no sentido anti-hor√°rio (10)
    delay(3000);
    
    
    digitalWrite(motor_pin_one, LOW);
    digitalWrite(motor_pin_two, LOW);     // motor desligado (00 ou 11)
  }
```

